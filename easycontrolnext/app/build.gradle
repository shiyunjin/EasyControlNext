plugins {
  id 'com.android.application' version '8.13.0'
}

def keystorePropertiesFile = rootProject.file('../keystore.properties')
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
  keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
  namespace = 'com.shiyunjin.easycontrolnext.app'
  compileSdk 34

  defaultConfig {
    applicationId "com.shiyunjin.easycontrolnext.app"
    minSdk 21
    targetSdk 34
    versionCode 10006
    versionName '1.0.6'
    ndk {
      abiFilters "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
    }
  }

  viewBinding {
    enabled = true
  }
  
  signingConfigs {
      release {
        if (keystorePropertiesFile.exists()) {
          storeFile file(keystoreProperties['MY_KEYSTORE_FILE'])
          storePassword keystoreProperties['MY_KEYSTORE_PASSWORD']
          keyAlias keystoreProperties['MY_KEY_ALIAS']
          keyPassword keystoreProperties['MY_KEY_PASSWORD']
        } else {
          println "Keystore properties file not found. No signing configuration will be applied."
        }
      }
  }

  buildTypes {
    debug {
      buildConfigField "boolean", "ENABLE_DEBUG_FEATURE", "true"
      manifestPlaceholders = [app_name: "@string/app_name_dev", app_icon: "@mipmap/ic_launcher_dev", app_icon_round: "@mipmap/ic_launcher_dev_round"]
      debuggable true
    }
    release {
      buildConfigField "boolean", "ENABLE_DEBUG_FEATURE", "false"
      manifestPlaceholders = [app_name: "@string/app_name", app_icon: "@mipmap/ic_launcher", app_icon_round: "@mipmap/ic_launcher_round"]
      debuggable false
      minifyEnabled false
      shrinkResources = false
      proguardFiles 'proguard-rules.pro'
      signingConfig = signingConfigs.release
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  dependenciesInfo {
    // Disables dependency metadata when building APKs.
    includeInApk = false
    // Disables dependency metadata when building Android App Bundles.
    includeInBundle = false
  }

  packagingOptions {
    resources.excludes.add("META-INF/*")
  }
  buildFeatures {
    buildConfig = true
  }

}

dependencies {
  implementation 'androidx.documentfile:documentfile:1.1.0'
  implementation 'org.conscrypt:conscrypt-android:2.5.3'
  implementation 'com.github.MuntashirAkon:sun-security-android:1.1'

  implementation "androidx.annotation:annotation:1.9.1"
  implementation 'org.bouncycastle:bcprov-jdk15to18:1.82'
  implementation 'com.github.MuntashirAkon.spake2-java:spake2-android:2.2.0'
}
